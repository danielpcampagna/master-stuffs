prefix : <http://example.org/SAPOS#>
prefix gdprov: <https://w3id.org/GDPRov#>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix pPlan: <http://purl.org/net/p-plan#>
prefix prov: <http://www.w3.org/ns/prov#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix tcu: <http://example.org/TrinityCollegeDublin#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT 
    ?process ?collectedData ?dataCollectionActivity ?startedAt ?comparisonActivity ?comparisonEndedAt ?resultComparison ?rectificationActivity ?rectificationEndedAt
WHERE {
    # Prospective Graph
    ?process rdf:type/rdfs:subClassOf* gdprov:RectificationProcess .
    ?dataCollection rdf:type/rdfs:subClassOf* gdprov:DataCollectionStep .
    ?dataComparison rdf:type/rdfs:subClassOf* gdprov:DataComparisonStep .
    ?collectedData rdf:type/rdfs:subClassOf* gdprov:PersonalData . # TODO: Deve ser opcional e representar a instancia do PersonalData (trazer os dados da ALce)

    ?dataCollection gdprov:isPartOfProcess ?process .
    ?dataComparison gdprov:isPartOfProcess ?process .

    ?dataCollection pPlan:isPrecededBy+ ?dataComparison .

    OPTIONAL { ?dataCollection gdprov:generatesData ?collectedData . }
    OPTIONAL { ?dataComparison gdprov:usesData ?collectedData . }

    OPTIONAL {
        ?dataRectification rdf:type/rdfs:subClassOf* gdprov:RectifyData .
        ?dataRectification gdprov:isPartOfProcess ?process .
        ?dataComparison pPlan:isPrecededBy ?dataRectification .
    }

    # Retrospective Graph
    OPTIONAL {
        ?dataCollectionActivity rdf:type/rdfs:subClassOf* gdprov:DataCollectionActivity .
        ?comparisonActivity rdf:type/rdfs:subClassOf* gdprov:DataComparisonActivity .

        ?dataCollectionActivity pPlan:correspondsToStep ?dataCollection .
        ?comparisonActivity pPlan:correspondsToStep ?dataComparison .
        
        ?comparisonActivity prov:wasInformedBy+ ?dataCollectionActivity .

        ?dataCollectionActivity prov:startedAtTime ?startedAt .
        ?comparisonActivity prov:endedAtTime ?comparisonEndedAt .

        ?comparisonActivity gdprov:hadDataDiscrepancy ?resultComparison .


        OPTIONAL {
            ?rectificationActivity rdf:type/rdfs:subClassOf* gdprov:RectifyDataActivity .
            ?rectificationActivity pPlan:correspondsToStep ?dataRectification .
            ?rectificationActivity prov:wasInformedBy+ ?comparisonActivity .
            ?rectificationActivity prov:endedAtTime ?rectificationEndedAt.
        }
    }
}
ORDER BY ?process ?collectedData ?startedAt ?comparisonEndedAt ?rectificationEndedAt